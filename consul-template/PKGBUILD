# Maintainer: Tyler Langlois <ty |at| tjll |dot| net>
# Contributor: Tim Meusel <tim@bastelfreak.de>
pkgname=consul-template
pkgver=0.19.4
pkgrel=1
pkgdesc='Template rendering, notifier, and supervisor for HashiCorp Consul and Vault data'
arch=('i686' 'x86_64' 'armv7h' 'armv6h')
url='https://github.com/hashicorp/consul-template'
license=('MPL')
backup=("etc/$pkgname/config.hcl")
makedepends=('go>=1.8')
optdepends=('consul: interpolate values from a distributed key/value store'
            'vault: reference secure secrets in template files')
options=('!strip')
source=("$url/archive/v$pkgver.tar.gz"
        "$pkgname.service")
sha512sums=('b9ccfcdbc2a7bfb040de25a7f78d38c4098793d4de4f4be61f339f1bae0f38b32da008147ac1ffb449e7f78ecc8d47553c6cd02e29ce37bd30b38009fa2e6deb'
            '8b187ff470fb10b47b815b2faaad836ac369071c8ce7e353ec0cbc98e3b1ac2ffc9c892244ac492be1285caa303c4b5fd0a22df3bdb2a037fca1b06c7b24084b')

prepare() {
    # Prepare an empty GOPATH for `go build`
    mkdir -p "$srcdir/gopath"

    # Link extracted release into GOPATH
    mkdir -p "$srcdir/gopath/src/github.com/hashicorp"
    ln -sf "$srcdir/$pkgname-$pkgver" \
        "$srcdir/gopath/src/github.com/hashicorp/$pkgname"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"

    # Build flags copied from upstream build scripts
    GOPATH="$srcdir/gopath" \
        go build \
        -ldflags="-s -w \
            -X $pkgname/version.Name=$pkgname \
            -X $pkgname/version.Version=$pkgver" \
        -a -o "$pkgname" .
}

package() {
    mkdir -p "$pkgdir/etc/$pkgname"
    touch "$pkgdir/etc/$pkgname/config.hcl"

    install -D -m755 "$srcdir/$pkgname-$pkgver/$pkgname" \
                     "$pkgdir/usr/bin/$pkgname"
    install -D -m644 "$srcdir/$pkgname.service" \
                     "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}

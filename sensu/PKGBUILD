# Maintainer: Tyler Langlois <ty |at| tjll |dot| net>

pkgname=sensu
pkgver=0.22.2
pkgrel=1
pkgdesc='A monitoring framework that aims to be simple, malleable, and scalable.'
arch=('any')
url="https://${pkgname}app.org/"
license=('MIT')
depends=('ruby' 'ruby-bundler')
optdepends=('rabbitmq: required to run sensu-server'
            'redis: required to run sensu-server')
options=('!strip')
install="${pkgname}.install"

source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        "https://github.com/${pkgname}/${pkgname}-build/archive/${pkgver}-1.tar.gz"
        "${pkgname}.sysusers")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "$srcdir/$pkgname-build-$pkgver-1"

  # Let journald handle logging - sensu defaults logging to STDOUT.
  for service in sensu-{api,client,server} ; do
    sed -i "s| -l /var/log/sensu/${service}.log||" \
           sensu_configs/systemd/${service}.service
  done
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  bundle install --without development --binstubs --path vendor
}

package() {
  cd "$srcdir"

  install -dm755 "$pkgdir"/etc/sensu/{plugins,mutators,handlers,extensions} \
                 "$pkgdir"/opt \
                 "$pkgdir"/usr/bin

  cp -a --no-preserve=ownership,timestamp \
        $pkgname-$pkgver "$pkgdir"/opt/$pkgname
  cp -a --no-preserve=ownership,timestamp \
        $pkgname-build-$pkgver-1/sensu_configs/sensu/* \
        "$pkgdir"/etc/$pkgname/

  for service in sensu-{api,client,server} ; do
    install -Dm644 $pkgname-build-$pkgver-1/sensu_configs/systemd/${service}.service \
                   "$pkgdir"/usr/lib/systemd/system/${service}.service
  done

  # Provide globally-executable install executable for plugins.
  ln -s ../../opt/$pkgname/bin/$pkgname-install \
        "$pkgdir"/usr/bin/$pkgname-install

  # See man page for sysusers.d(5)
  install -D -m644 "$pkgname.sysusers" \
                   "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
}
